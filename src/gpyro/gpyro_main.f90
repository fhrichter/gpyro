PROGRAM GPYRO_STANDALONE

USE PREC
USE GPYRO_PYRO
USE GPYRO_VARS
USE GPYRO_IO
USE GPYRO_INIT
USE GPYRO_FUNCS

IMPLICIT NONE

INTEGER  :: ICASE,IMESH,NTIMESTEPS,LO10
REAL(EB) :: TI, TSTART,TEND
LOGICAL  :: LOPEN, FAILED_TS
CHARACTER(300) :: MESSAGE

! Initialize system clock
CALL SYSTEM_CLOCK(COUNT_RATE=CLOCK_COUNT_RATE)
CALL SYSTEM_CLOCK(COUNT_MAX=CLOCK_COUNT_MAX)

GPG%GPYRO_WALL_CLOCK_START = WALL_CLOCK_TIME_THANKS_FDS()

WRITE(*,*) 'Gpyro 0.8200'
WRITE(*,*) 'Build date:  August 29, 2020'
WRITE(*,*) 'https://github.com/reaxfire/gpyro'
WRITE(*,*)

IGPYRO_TYPE = 1 !Standalone implementation

CALL READ_GPYRO
CALL CHECK_GPYRO(0) !Checks for potential conditions that could lead to seg fault or cause other problems
      
DO ICASE = 1, GPG%NCASES

   IMESH = GPG%IMESH(ICASE)
   CALL ALLOCATE_GPYRO(IMESH)
   G=>GPM(IMESH)
   GPBCP(1:,1:,1:,-3:)=>G%GPYRO_BOUNDARY_CONDITION(1:,1:,1:,-3:)

   CALL INIT_GPYRO_VARS(ICASE,IMESH)

! If summary file is open, close it:
   INQUIRE(UNIT=LUSUM,OPENED=LOPEN) ; IF(LOPEN) CLOSE(LUSUM)

   CALL DUMP_GPYRO(IMESH,ICASE,0D0)
   IF (GPG%FDSMODE) CALL DUMP_FDS_INPUT_FILE(ICASE)

   GPG%DT     = GPG%DT0     
   TI         = GPG%DT0
   NTIMESTEPS = 0

   CALL GET_CPU_TIME(TSTART)

   WRITE(*,*) 'Entering main timestepping loop.' 

   DO WHILE (TI .LE. GPG%TSTOP(ICASE))
      IF (TI .GT. GPG%TSTOP(ICASE)) CYCLE

      FAILED_TS = .TRUE.
      DO WHILE (FAILED_TS)
         GPG%DT = GPG%DTNEXT
         IF (GPG%ZEROD(ICASE)) THEN
            CALL TG_DRIVER(ICASE,TI,FAILED_TS,G%TPN(1,1,1))
         ELSE
            CALL GPYRO_PYROLYSIS(IMESH,ICASE,G%NCELLZ,G%NCELLX,G%NCELLY,TI,FAILED_TS)
            IF(G%DELTAN(1,1) .LT. 5D-4*G%ZDIM) THEN 
               TI = GPG%TSTOP(ICASE) + 1.
               CONTINUE
            ENDIF
         ENDIF
      ENDDO
     
      NTIMESTEPS = NTIMESTEPS + 1
      LO10 = LOG10(REAL(MAX(1,ABS(NTIMESTEPS)),EB)) !Borrow a little trick from FDS
      IF (MOD(NTIMESTEPS,10**LO10) .EQ. 0 .OR. MOD(NTIMESTEPS,100)==0 .OR. TI .GE. GPG%TSTOP(ICASE) .OR. &
         GPG%MAXTMP .GT. GPG%TMP_REDUCED_DTDUMP) THEN
         WRITE(*,99) 'Timestep #: ', NTIMESTEPS, '  Time: ', TI, ' s'
         CALL WRITE_DOTOUT_FILE(ICASE,TI,IMESH) 
      ENDIF

      IF (GPG%MAXTMP.NE.GPG%MAXTMP .OR. GPG%MAXTMP.EQ.GPG%POSINF .OR. GPG%MAXTMP.EQ.GPG%NEGINF) CALL WRITE_DOTOUT_FILE(ICASE,TI,IMESH) 
      GPG%DT = GPG%DTNEXT
      TI     = TI + GPG%DT
 
   ENDDO ! TIME LOOP

   CALL GET_CPU_TIME(TEND)
   GPG%TUSED(0) = GPG%TUSED(0) + TEND - TSTART

   CALL DEALLOCATE_GPYRO

ENDDO !ICASE

MESSAGE = 'End of simulation reached successfully. Shutting down.' 
CALL SHUTDOWN_GPYRO(MESSAGE)

 99   FORMAT(A,I7,A,F13.4,A)

END PROGRAM GPYRO_STANDALONE